// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input
 * holds the value of this location. If the current instruction needs
 * to write a value to M, the value is placed in outM, the address
 * of the target location is placed in the addressM output, and the
 * writeM control bit is asserted. (When writeM==0, any value may
 * appear in outM). The outM and writeM outputs are combinational:
 * they are affected instantaneously by the execution of the current
 * instruction. The addressM and pc outputs are clocked: although they
 * are affected by the execution of the current instruction, they commit
 * to their new values only in the next time step. If reset==1 then the
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather
 * than to the address resulting from executing the current instruction.
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M?
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    /* Load A? */
    Not(in=instruction[15], out=loadAtmp);
    And(a=instruction[15], b=instruction[5], out=loadAtmp2);
    Or(a=loadAtmp, b=loadAtmp2, out=loadA);

    Mux16(a=instruction, b=AluOut, sel=instruction[15], out=Ains);

    ARegister(in=Ains, load=loadA, out=Aout);
    ARegister(in=Ains, load=loadA, out[0..14]=addressM);


    /* Dregister */
    DRegister(in=AluOut, load=instruction[4], out=Dout);

    /* WriteM? */
    And(a=instruction[3], b=instruction[15], out=writeM);


    /* y = A or y = M (comp) */
    Mux16(a=Aout, b=inM, sel=instruction[12], out=cmp);

    ALU(x=Dout, y=cmp, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=AluOut, zr=isZero, ng=isNegative);
    Mux16(a=false, b=AluOut, sel=instruction[15], out=outM);

    /* Program counter */
    Not(in=isNegative, out=postmp);
    Not(in=isZero, out=postmp2);
    And(a=postmp, b=postmp2, out=isPositive); 

    And(a=instruction[15], b=instruction[0], out=jmp0);
    And(a=instruction[15], b=instruction[1], out=jmp1);
    And(a=instruction[15], b=instruction[2], out=jmp2);

    And(a=jmp0, b=isPositive, out=jmpGreater);  // jump if greater

    And(a=jmp1, b=isZero, out=jmpZero); // jump if is zero

    And(a=jmp2, b=isNegative, out=jmpLess); // jump if less


    Or(a=jmpGreater, b=jmpLess, out=jmpDiff);
    Or(a=jmpDiff, b=jmpZero, out=jmp);

    PC(in=Aout, load=jmp, inc=true, reset=reset, out[0..14]=pc);
}
